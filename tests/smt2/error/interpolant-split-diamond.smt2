(declare-sort ID)
(declare-const a0 ID)
(declare-const a1 ID)
(declare-const a01 Bool)
(declare-const a1b Bool)
(declare-const a2 ID)
(declare-const a02 Bool)
(declare-const a2b Bool)
(declare-const b0 ID)
(declare-const b1 ID)
(declare-const b01 Bool)
(declare-const b1c Bool)
(declare-const b2 ID)
(declare-const b02 Bool)
(declare-const b2c Bool)
(declare-const c0 ID)
(declare-const c1 ID)
(declare-const c01 Bool)
(declare-const c1d Bool)
(declare-const c2 ID)
(declare-const c02 Bool)
(declare-const c2d Bool)
(declare-const d0 ID)
(declare-const d1 ID)
(declare-const d01 Bool)
(declare-const d1e Bool)
(declare-const d2 ID)
(declare-const d02 Bool)
(declare-const d2e Bool)
(declare-const e0 ID)
;    a1    b1    c1    d1
;   /  \  /  \  /  \  /  \
; a0    b0    c0    d0    e0
;   \  /  \  /  \  /  \  /
;    a2    b2    c2    d2
(assert (! (=> a01 (= a0 a1)) :named a01*))
(assert (! (=> a1b (= a1 b0)) :named a1b*))
(assert (! (=> a02 (= a0 a2)) :named a02*))
(assert (! (=> a2b (= a2 b0)) :named a2b*))
(assert (! (=> b01 (= b0 b1)) :named b01*))
(assert (! (=> b1c (= b1 c0)) :named b1c*))
(assert (! (=> b02 (= b0 b2)) :named b02*))
(assert (! (=> b2c (= b2 c0)) :named b2c*))
(assert (! (=> c01 (= c0 c1)) :named c01*))
(assert (! (=> c1d (= c1 d0)) :named c1d*))
(assert (! (=> c02 (= c0 c2)) :named c02*))
(assert (! (=> c2d (= c2 d0)) :named c2d*))
(assert (! (=> d01 (= d0 d1)) :named d01*))
(assert (! (=> d1e (= d1 e0)) :named d1e*))
(assert (! (=> d02 (= d0 d2)) :named d02*))
(assert (! (=> d2e (= d2 e0)) :named d2e*))
(assert (! (not (= a0 e0)) :named ne))
(assert (! (or (and a01 a1b) (and a02 a2b)) :named ab))
(assert (! (or (and b01 b1c) (and b02 b2c)) :named bc))
(assert (! (or (and c01 c1d) (and c02 c2d)) :named cd))
(assert (! (or (and d01 d1e) (and d02 d2e)) :named de))
(define-const eqab Bool (= a0 b0))
(check-sat)
(get-interpolants (and a01* a02* b1c* b2c* c01* c02* d1e* d2e* ne ab bc cd de) (and a1b* a2b* b01* b02* c1d* c2d* d01* d02*))
(get-interpolants (and a01* a02* b1c* b2c* c01* c02* d1e* d2e* ne) (and a1b* a2b* b01* b02* c1d* c2d* d01* d02* ab bc cd de))
(get-interpolants (and a1b* a2b* b01* b02* c1d* c2d* d01* d02*) (and a01* a02* b1c* b2c* c01* c02* d1e* d2e* ne ab bc cd de))
(get-interpolants (and a1b* a2b* b01* b02* c1d* c2d* d01* d02* ab bc cd de) (and a01* a02* b1c* b2c* c01* c02* d1e* d2e* ne))